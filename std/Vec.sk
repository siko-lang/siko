module Vec {

import Ptr

class InnerVec[T] {
    items: *T,
    size: Int,
    capacity: Int

    fn new() -> InnerVec[T] {
        InnerVec(null(), 0, 0)
    }

    fn push(mut self, item: T) -> Self {
        if self.size == self.capacity {
            if self.capacity == 0 {
                self.grow(8);
            } else {
                self.grow(self.capacity * 2);
            }
        }
        let ptr = offset(self.items, self.size);
        store(ptr, item);
        self.size = self.size + 1;
        self
    }

    fn get(&self, index: Int) -> &T {
        if self.size <= index {
            panic("OutOfBounds");
        }
        self.getUnchecked(index)
    }

    fn getUnchecked(&self, index: Int) -> &T {
        let ptr = offset(self.items, index);
        toRef(ptr)
    }

    fn grow(mut self, capacity: Int) -> Self {
        self.capacity = capacity;
        let newItems: *T = allocateArray(self.capacity);
        if self.size > 0 {
            memcpy(newItems, self.items, self.size);
        }
        self.items = newItems;
        self
    }
}

class Vec[T] {
    inner: InnerVec[T]

    fn new() -> Vec[T] {
        Vec(InnerVec.new())
    }

    fn push(mut self, item: T) -> Self {
        self.inner = self.inner.push(item); // TODO: mut self should apply to self.inner.push as well?!
        self
    }

    fn get(&self, index: Int) -> &T {
        self.inner.get(index)
    }

    fn getUnchecked(&self, index: Int) -> &T {
        self.inner.getUnchecked(index)
    }

    fn iter(&self) -> VecIteratorRef[T] {
        VecIteratorRef(&self, 0)
    }

    fn intoIter(self) -> VecIterator[T] {
        VecIterator(self.inner, 0)
    }

    fn len(&self) -> Int {
        self.inner.size
    }
}

class VecIterator[T] {
    inner: InnerVec[T],
    index: Int
}

instance[T] Drop[VecIterator[T]] {
    fn drop(mut self) -> Self {
        if self.inner.capacity == 0 {
            return self
        }
        loop {
            if self.index >= self.inner.size {
                break;
            }
            let ptr = offset(self.inner.items, self.index);
            self.index = self.index + 1;
            let item = Ptr.load(ptr);
        }
        deallocate(self.inner.items);
        self.inner.items = null();
        self
    }
}

instance[T] Iterator[VecIterator[T]] {
    type Item = T

    fn next(mut self) -> (Self, Option[T]) {
        if self.index >= self.inner.size {
            return (self, None)
        }
        let ptr = offset(self.inner.items, self.index);
        self.index = self.index + 1;
        let item = Ptr.load(ptr);
        (self, Some(item))
    }
}

instance[T] IntoIterator[Vec[T]] {
    type Item = T
    type Iter = VecIterator[T]

    fn intoIterator(self) -> VecIterator[T] {
        self.intoIter()
    }
}

class VecIteratorRef[T] {
    vec: &Vec[T]
    index: Int
}

instance[T] Iterator[VecIteratorRef[T]] {
    type Item = &T

    fn next(mut self) -> (Self, Option[&T]) {
        if self.index >= self.vec.inner.size {
            return (self, None)
        }
        let item = self.vec.getUnchecked(self.index);
        self.index = self.index + 1;
        (self, Some(item))
    }
}

instance[T] Drop[Vec[T]] {
    fn drop(mut self) -> Self {
        {
            let iter = VecIterator(self.inner, 0);
            self.inner = InnerVec.new();
        }
        self
    }
}

}