module Vec {

import NativePtr

struct InnerVec[T] {
    items: *T,
    size: Int,
    capacity: Int

    fn new() -> InnerVec[T] {
        InnerVec(null(), 0, 0)
    }

    fn push(mut self, item: T) -> Self {
        if self.size == self.capacity {
            if self.capacity == 0 {
                self.grow(8);
            } else {
                self.grow(self.capacity * 2);
            }
        }
        let ptr = offset(self.items, self.size);
        store(ptr, item);
        self.size = self.size + 1;
        self
    }

    fn get(&self, index: Int) -> &T {
        if self.size <= index {
            panic("OutOfBounds");
        }
        self.getUnchecked(index)
    }

    fn getUnchecked(&self, index: Int) -> &T {
        let ptr = offset(self.items, index);
        toRef(ptr)
    }

    fn grow(mut self, capacity: Int) -> Self {
        self.capacity = capacity;
        let newItems: *T = allocateArray(self.capacity);
        if self.size > 0 {
            memcpy(newItems, self.items, self.size);
        }
        self.items = newItems;
        self
    }
}

pub struct Vec[T] {
    inner: InnerVec[T]

    pub fn new() -> Vec[T] {
        Vec(InnerVec.new())
    }

    pub fn push(mut self, item: T) -> Self {
        self.inner = self.inner.push(item); // TODO: mut self should apply to self.inner.push as well?!
        self
    }

    pub fn get(&self, index: Int) -> &T {
        self.inner.get(index)
    }

    pub fn getUnchecked(&self, index: Int) -> &T {
        self.inner.getUnchecked(index)
    }

    pub fn iter(&self) -> VecIteratorRef[T] {
        VecIteratorRef(&self, 0)
    }

    pub fn intoIter(self) -> VecIterator[T] {
        VecIterator(self.inner, 0)
    }

    pub fn len(&self) -> Int {
        self.inner.size
    }

    pub fn swap(self, i: Int, j: Int) -> Self {
        if i == j {
            return self;
        }
        let ptr = offset(self.inner.items, i);
        let ptr2 = offset(self.inner.items, j);
        let item = *ptr;
        let item2 = *ptr2;
        *ptr = item2;
        *ptr2 = item;
        self
    }

    pub fn reverse(mut self) -> Self {
        let mut i = 0;
        let mut j = self.len() - 1;
        while i < j {
            self.swap(i, j);
            i = i + 1;
            j = j - 1;
        }
        self
    }

    pub fn asPtr(&self) -> *T {
        self.inner.items
    }
}

struct VecIterator[T] {
    inner: InnerVec[T],
    index: Int
}

instance[T] Drop[VecIterator[T]] {
    fn drop(mut self) -> Self {
        if self.inner.capacity == 0 {
            return self
        }
        loop {
            if self.index >= self.inner.size {
                break;
            }
            let ptr = offset(self.inner.items, self.index);
            self.index = self.index + 1;
            let item = *ptr;
        }
        deallocate(self.inner.items);
        self.inner.items = null();
        self
    }
}

instance[T] Iterator[VecIterator[T]] {
    type Item = T

    fn next(mut self) -> (Self, Option[T]) {
        if self.index >= self.inner.size {
            return (self, None)
        }
        let ptr = offset(self.inner.items, self.index);
        self.index = self.index + 1;
        let item = *ptr;
        (self, Some(item))
    }
}

instance[T] IntoIterator[Vec[T]] {
    type Item = T
    type Iter = VecIterator[T]

    fn intoIterator(self) -> VecIterator[T] {
        self.intoIter()
    }
}

struct VecIteratorRef[T] {
    vec: &Vec[T]
    index: Int
}

instance[T] Iterator[VecIteratorRef[T]] {
    type Item = &T

    fn next(mut self) -> (Self, Option[&T]) {
        if self.index >= self.vec.inner.size {
            return (self, None)
        }
        let item = self.vec.getUnchecked(self.index);
        self.index = self.index + 1;
        (self, Some(item))
    }
}

instance[T] Drop[Vec[T]] {
    fn drop(mut self) -> Self {
        {
            let iter = VecIterator(self.inner, 0);
            self.inner = InnerVec.new();
        }
        self
    }
}

}